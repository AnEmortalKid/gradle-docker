plugins {
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'com.palantir.git-version' version '0.1.3'
    id 'eclipse'
    id 'groovy'
    id 'idea'
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
        exclude module: 'groovy-all'
    }
    testCompile files(createClasspathManifest)
}

group 'com.palantir.gradle.docker'
version gitVersion()

pluginBundle {
    website = 'https://github.com/palantir/gradle-docker'
    vcsUrl = 'https://github.com/palantir/gradle-docker'
    description = 'Gradle Docker is a simple docker orchestrator for executing docker build and push from within Gradle.'
    tags = ['docker']

    plugins {
        dockerPlugin {
            id = 'com.palantir.docker'
            displayName = 'Palantir Gradle Docker'
        }
    }
}

publishPlugins.onlyIf {
    project.version ==~ /\d+\.\d+\.\d+/
}

